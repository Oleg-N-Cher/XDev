;-------------------------------------------------
;.include "spr_const.h"
;-------------------------------------------------
; Стандартный экран 256x192, знакоместа,
; атрибуты посимвольно
; Вывод спрайта без маски и без атрибутов!
;-------------------------------------------------
; Формат спрайта:
;    IX+0,1    - количество байт в спрайте SSZ, включая длину заголовка.
;        SSZ = W*H*8 + 5 байт
;    IX+2    - ширина спрайта W (знакоместа)
;    IX+3    - высота спрайта H (знакоместа)
;    IX+4    - W*H*8 байт - данные спрайта построчно
;
;    - Данные распологаются построчно -
;
;    0 ...... W-1    ;
;    0 ...... W-1    ;
;      ......    ; H строк
;    0 ...... W-1    ;
;-------------------------------------------------
    .globl sprXYtoHL
    .globl sprXYtoHLattr

        .module spr0_out0_s
    .area    _CODE
;-------------------------------------------------
; Процедура вывода спрайта без атрибутов. Базовая 
; Вход:
;    L=x, H=y
;    IX - Адрес спрайта
;-------------------------------------------------
.globl spr0_out0
spr0_out0:
    ; Определяем адрес в экранной области
    call    sprXYtoHL

    ; DE - данные спрайта
    push    ix
    ld    de,#4
    add    ix,de
    push    ix
    pop    de
    pop    ix

    ; Высота спрайта
    ld    b,3(ix)
spr0_out0_hline:    ; Начало цикла по строкам
    push    bc

    ; высота знакоместа (строки)
    ld    c,#8
    push    hl

    ; выводим 8 линий (одну строку)
spr0_out0_line8:
    ld    b,2(ix)

    ; выводим w байт (одну линию)
    push    hl
spr0_out0_line:
    ld    a,(de)
    ; Вместо nop можно воткнуть xor (hl), and (hl) или or (hl)
spr0_out0_opt:
    nop
    ;
    ld    (hl),a
    inc    hl
    inc    de
    dec    b
    jr nz,    spr0_out0_line
    pop    hl

    ; следующая линия экрана
    inc    h
    dec    c
    jr nz,    spr0_out0_line8
    pop    hl

    ; Следующая строка
    ld    a,#0x20
    add    a,l
    ld    l,a

    ; Если был перенос - то переход к след. трети экрана
    jr    nc, continue_1_3

    ; следующая треть экрана
    ld    a,#0x08
    add    a,h
    ld    h,a        ; hl = hl + 0x0800

continue_1_3:
    ; Вывод в ту же треть экрана
    pop    bc
    djnz    spr0_out0_hline    ; Конец цикла по строкам (та же треть)
    ; Выход, если всё
    ret
;-------------------------------------------------
; Вывод без операции (наложение с затиранием)
;-------------------------------------------------
.globl _spr0_out0_setopt_nop
_spr0_out0_setopt_nop:
    ld a,#0x00
    jr spr0_out0_setopt_ep
;-------------------------------------------------
; Вывод AND
;-------------------------------------------------
.globl _spr0_out0_setopt_and
_spr0_out0_setopt_and:
    ld a,#0xA6    ; and (hl)
    jr spr0_out0_setopt_ep
;-------------------------------------------------
; Вывод OR
;-------------------------------------------------
.globl _spr0_out0_setopt_or
_spr0_out0_setopt_or:
    ld a,#0xB6    ; or (hl)
    jr spr0_out0_setopt_ep
;-------------------------------------------------
; Вывод XOR
;-------------------------------------------------
.globl _spr0_out0_setopt_xor
_spr0_out0_setopt_xor:
    ld a,#0xAE    ; xor (hl)
    jr spr0_out0_setopt_ep
;-------------------------------------------------
spr0_out0_setopt_ep:
    ld (spr0_out0_opt),a
    ret
;-------------------------------------------------
; Вывод атрибутов спрайта
; Вход:
;    IX - Адрес спрайта
;    L=x, H=y
;    DE - адрес атрибутов
spr0_outattr0:
    call    sprXYtoHLattr
    ; Высота спрайта
    ld    b,3(ix)
    ld    c,2(ix)
spr0_outattr0_hline:    ; Начало цикла по строкам
    ; линия
    push    bc
    push    hl

spr0_outattr0_line:
    ld    a,(de)
    ld    (hl),a
    inc    de
    inc    hl
    dec    c
    jr nz, spr0_outattr0_line

    pop    hl
    ld    bc,#0x0020
    add    hl,bc
    pop    bc
    djnz    spr0_outattr0_hline
    ;
    ret
;-------------------------------------------------
; Процедура вывода спрайта с атрибутами
; Вход:
;    L=x, H=y
;    IX - Адрес спрайта
;-------------------------------------------------
.globl spr0_out0_attr
spr0_out0_attr:
    push    hl
    call    spr0_out0
    pop    hl
    jp    spr0_outattr0
;-------------------------------------------------
